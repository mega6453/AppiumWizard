; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Appium Wizard"
#define MyAppVersion "1.0.0"
#define MyAppPublisher "Meganathan C"
#define MyAppExeName "Appium Wizard.exe"

[Setup]
AlwaysRestart = yes 
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{408E2B01-BCF1-40E3-8F27-F11B32572D03}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={autopf}\{#MyAppName}
DisableDirPage=yes
DisableProgramGroupPage=yes
;LicenseFile=C:\Users\mc\Desktop\Appium Wizard\License.txt
;InfoBeforeFile=C:\Users\mc\Desktop\Appium Wizard\Information.txt
; Remove the following line to run in administrative install mode (install for all users.)
PrivilegesRequired=lowest
OutputDir=D:\a\AppiumWizard\AppiumWizard\
OutputBaseFilename=Appium Wizard
;SetupIconFile=Release\Resources\Images\appiumlogo.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "D:\a\AppiumWizard\AppiumWizard\Appium Wizard\bin\Release\net6.0-windows\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\a\AppiumWizard\AppiumWizard\Appium Wizard\bin\Release\net6.0-windows\Resources\*"; DestDir: "{app}\Resources\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "D:\a\AppiumWizard\AppiumWizard\Appium Wizard\bin\Release\net6.0-windows\runtimes\*"; DestDir: "{app}\runtimes\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "D:\a\AppiumWizard\AppiumWizard\Appium Wizard\bin\Release\net6.0-windows\Appium Wizard.pdb"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\a\AppiumWizard\AppiumWizard\Appium Wizard\bin\Release\net6.0-windows\appiumlogo.ico"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\a\AppiumWizard\AppiumWizard\Appium Wizard\bin\Release\net6.0-windows\D3DCompiler_47_cor3.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\a\AppiumWizard\AppiumWizard\Appium Wizard\bin\Release\net6.0-windows\PenImc_cor3.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\a\AppiumWizard\AppiumWizard\Appium Wizard\bin\Release\net6.0-windows\PresentationNative_cor3.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\a\AppiumWizard\AppiumWizard\Appium Wizard\bin\Release\net6.0-windows\sni.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\a\AppiumWizard\AppiumWizard\Appium Wizard\bin\Release\net6.0-windows\SQLite.Interop.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\a\AppiumWizard\AppiumWizard\Appium Wizard\bin\Release\net6.0-windows\vcruntime140_cor3.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\a\AppiumWizard\AppiumWizard\Appium Wizard\bin\Release\net6.0-windows\WebView2Loader.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\a\AppiumWizard\AppiumWizard\Appium Wizard\bin\Release\net6.0-windows\wpfgfx_cor3.dll"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Code]
var
  ProgressPage: TOutputProgressWizardPage;

function GetWindowsAppsPath: string;
var
  Username: string;
begin
  // Get the current user's username
  Username := GetUserNameString;

  // Construct the path to the WindowsApps folder
  Result := 'C:\Users\' + Username + '\AppData\Local\Microsoft\WindowsApps';
end;

procedure InitializeWizard;
begin
  // Create a custom progress page
  ProgressPage := CreateOutputProgressPage('Installing', 'Please wait while Installing WSL, Node.js, Appium Server and Appium drivers. Please provide necessary permission when system prompts.');
  ProgressPage.SetProgress(0, 100);
end;

function NextButtonClick(CurPageID: Integer): Boolean;
var
 ResultCode: Integer;
  ErrorCode: Integer;
  AppiumPath: string;
  WindowsAppsPath: string;
  Progress: Integer;
begin
  Result := True;

  if CurPageID = wpReady then
  begin
    // Display the message box
    if MsgBox('Please provide necessary permissions when system prompts for proper installation.', mbInformation, MB_OKCANCEL) = IDOK then
    begin
      // Run the winget command to install Node.js silently
      // for my reference - SW_HIDE - to hide the window, SW_SHOWNORMAL - to show the window
      ProgressPage.Show;
      ProgressPage.SetText('Installing Node.js...','');
      WindowsAppsPath := GetWindowsAppsPath; 
      if not FileExists(WindowsAppsPath + '\winget.exe') then
      begin
        MsgBox('The winget.exe file was not found.', mbError, MB_OK);
        Result := False;
        Exit;
      end;

      if not Exec(WindowsAppsPath + '\winget.exe', 'install OpenJS.NodeJS', '', SW_HIDE, ewWaitUntilTerminated, ResultCode) then
      begin
        MsgBox('Failed to install Node.js using winget. Error code: ' + IntToStr(ResultCode), mbError, MB_OK);
        Result := False;
        Exit;
      end;

      Progress := 20;
      ProgressPage.SetProgress(Progress, 100);

      // Add the command to install Appium globally using npm
      ProgressPage.SetText('Installing Appium Server...','');
      if not Exec('cmd', '/C npm install -g appium', '',  SW_HIDE, ewWaitUntilTerminated, ErrorCode) then
      begin
        MsgBox('Failed to install Appium globally. After installation completed, Run Appium Wizard to retry the appium installation.', mbError, MB_OK);
        Result := False;
        Exit;
      end;
      Progress := 40;
      ProgressPage.SetProgress(Progress, 100);

      // Get the full path to the appium executable
      AppiumPath := '"' + ExpandConstant('{userappdata}\npm\appium.cmd') + '"';

      // Install Appium drivers
      ProgressPage.SetText('Installing XCUITest driver for iOS...','');
      if not Exec('cmd', '/C "' + AppiumPath + ' driver install xcuitest"', '',  SW_HIDE, ewWaitUntilTerminated, ErrorCode) then
      begin
        MsgBox('Failed to install XCUITest. After installation completed, Run Appium Wizard to retry the xcuitest installation.', mbError, MB_OK);
        Result := False;
        Exit;
      end;
      Progress := 60;
      ProgressPage.SetProgress(Progress, 100);

      ProgressPage.SetText('Installing UIAutomator2 driver for Android...','');
      if not Exec('cmd', '/C "' + AppiumPath + ' driver install uiautomator2"', '',  SW_HIDE, ewWaitUntilTerminated, ErrorCode) then
      begin
        MsgBox('Failed to install UIAutomator2. After installation completed, Run Appium Wizard to retry the UIAutomator2 installation.', mbError, MB_OK);
        Result := False;
        Exit;
      end;
      Progress := 80;
      ProgressPage.SetProgress(Progress, 100);

      ProgressPage.SetText('Installing WSL (for iOS app signing), Please provide necessary permission when system prompts...','');
      if not Exec('cmd', '/C C:\Windows\Sysnative\wsl.exe --install --no-distribution', '', SW_HIDE, ewWaitUntilTerminated, ErrorCode) then
      begin
        MsgBox('Failed to install WSL.', mbError, MB_OK);
        Result := False;
        Exit;
      end;
      Progress := 100;
      ProgressPage.SetProgress(Progress, 100);         
      //ProgressPage.SetText('Installation completed!','');
      //MsgBox('Please Restart the system and Launch Appium Wizard.', mbInformation, MB_OK);
      Result := True;   
      progressPage.Hide;
    end
    else
    begin
      Result := False;
    end;
  end;
end;
[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent
